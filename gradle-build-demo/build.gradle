plugins {
    // Java插件
    id 'java'
    // 覆盖率工具
    id 'jacoco'
    // 维护springboot版本号
    id 'org.springframework.boot' version '2.7.18'
    // 进行依赖管理,在引入其它boot依赖时省略版本号、解决jar包冲突问题
    id 'io.spring.dependency-management' version '1.1.4'
    // 引入grgit插件,用于git操作
    id 'org.ajoberstar.grgit' version '4.1.1'
}

// 项目信息
group 'cc.taketo'
version '1.0-SNAPSHOT'

// 仓库配置
repositories {
    jcenter()
    maven { url "https://maven.aliyun.com/repository/public/" }
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "https://maven.aliyun.com/repository/google/" }
    maven { url "https://maven.aliyun.com/repository/apache-snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
}

// 项目依赖
dependencies {
    // web 开发
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // 日志
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    // 测试
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// 配置项
configurations.all {
    // 排除自带日志，使用log4j2
    exclude module: "spring-boot-starter-logging"
}

// 构建任务配置
jar {
    // JAR输出目录
    destinationDir file('dist/app')
    // JAR文件名
    archiveName project.name + "-" + project.version + '.jar'
    exclude '**/*.xml'
    exclude '**/*.toml'
    exclude '**/*.properties'
    exclude '**/*.yml'

    manifest {
        try {
            // .git目录位置, 当前目录
//            def repo = grgit.open(dir: file('.').canonicalPath)
            // .git目录位置, 上级目录
            def repo = grgit.open(dir: file('..').canonicalPath)
            if (repo != null) {
                def date = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
                def branch = repo.branch.getCurrent().getName()
                def commit = repo.head().getAbbreviatedId(40)

                attributes(["Implementation-Timestamp": date,
                            "Git-Branch"              : branch,
                            "Git-Commit"              : commit
                ])

                logger.info("  Commit : ")
                logger.info("  => date: {}", date)
                logger.info("  => branch: {}", branch)
                logger.info("  => commit: {}", commit)
            }
        } catch (Exception e) {
            logger.warn(' .git not exist, cannot found commit info')
        }
    }

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf'
        }
        copy {
            from configurations.runtimeClasspath
            into 'dist/lib'
        }
        copy {
            // 拷贝当前文件夹所有以.sh或.env结尾的文件
            from file('.').listFiles().findAll { File f -> (f.name.endsWith('.sh') || f.name.endsWith('.env')) }
            into 'dist'
        }
        copy {
            from file('../scripts/')
            into 'dist'
        }
    }
}

// 测试任务配置
test {
    testLogging {
        // 不显示标准输出和错误流
        showStandardStreams = false
        // 只显示以下事件相关的日志信息
        events 'passed', 'skipped', 'failed'
    }
}

// Jacoco测试报告配置
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}